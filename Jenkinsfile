#!/usr/bin/env groovy

def platform2Repo = [
  // RedHat 8
  "almalinux8java8": "redhat8",
  // RedHat 9
  "almalinux9java8": "redhat9",
  "almalinux9java11": "redhat9",
  "almalinux9java17": "redhat9"
]

def buildRepoName(repo, platform) {

  def repoName
  if (platform ==~ /^almalinux\d+.*/) {
    repoName = "${repo}-rpm-${env.BRANCH_NAME}"
  } else {
    error("Unsupported platform: ${platform}")
  }
  echo "Repo name: $repoName"
  return repoName
}

def removePackages(repo, platform, platform2Repo) {

  def platformRepo = platform2Repo[platform]
  if (!platformRepo) {
    error("Unknown platform: $platform")
  }
  echo "platformRepo = $platformRepo"

  if (platform ==~ /^almalinux\d+.*/) {
    sh 'nexus-assets-remove -u $NEXUS_CRED_USR -p $NEXUS_CRED_PSW -H $NEXUS_HOST -r ' + repo + ' -q ' + platformRepo
  } else {
    error("Unsupported platform: $platform")
  }
}

def publish(repo, platform, platform2Repo) {

  def platformRepo = platform2Repo[platform]
  if (!platformRepo) {
    error("Unknown platform: $platform")
  }
  echo "platformRepo = $platformRepo"

  if (platform ==~ /^almalinux\d+.*/) {
    sh 'nexus-assets-flat-upload -u $NEXUS_CRED_USR -p $NEXUS_CRED_PSW -H $NEXUS_HOST -r ' + repo + '/' + platformRepo + ' -d artifacts/packages/' + platform + '/RPMS'
  } else {
    error("Unsupported platform: $platform")
  }
}

def doPlatform(repo, platform, platform2Repo) {
  return {
    def repoName = buildRepoName(repo, platform)

    if (env.CLEANUP_REPO) {
      removePackages(repoName, platform, platform2Repo)
    }

    publish(repoName, platform, platform2Repo)
  }
}

def publishPackages(releaseInfo,platform2Repo) {

  copyArtifacts filter: 'artifacts/**', fingerprintArtifacts: true, projectName: "${releaseInfo.project}", target: '.'

  def repo = releaseInfo.repo
  def platforms = releaseInfo.platforms

  echo "Publish for the following ${platforms}"

  def publishStages = platforms.collectEntries {
    [ "${env.BRANCH_NAME} - ${it}" : doPlatform(repo,it,platform2Repo) ]
  }

  parallel publishStages
}

def doPublish(platform2Repo) {
  def releaseInfo = readYaml file:'release-info.yaml'
  publishPackages(releaseInfo,platform2Repo)
}

pipeline {

  agent {
    label 'docker'
  }

  options {
    timeout(time: 10, unit: 'MINUTES')
  }

  environment {
    NEXUS_HOST = "https://repo.cloud.cnaf.infn.it"
    NEXUS_CRED = credentials('jenkins-nexus')
  }

  stages {

    stage('checkout') {
      steps {
        deleteDir()
        checkout scm
      }
    }

    stage('publish packages (nightly)') {

      environment {
        CLEANUP_REPO = "y"
      }

      when {
        branch 'nightly'
      }

      steps {
        script {
          doPublish(platform2Repo)
        }
      }
    }

    stage('publish packages (beta)') {

      environment {
        CLEANUP_REPO = "y"
      }

      when {
        branch 'beta'
      }

      steps {
        script {
          doPublish(platform2Repo)
        }
      }
    }

    stage('publish packages (stable)') {

      when {
        branch 'stable'
      }

      steps {
        script {
          doPublish(platform2Repo)
        }
      }
    }
  }
}
